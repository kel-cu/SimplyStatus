plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1" apply false
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.4-SNAPSHOT" apply false
    id "io.github.p03w.machete" version "2.+" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.p03w.machete"
    loom {
        silentMojangMappingsLicense()
    }
    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.mc_parchment_version}:${rootProject.parchment_version}@zip")
        }
        // Discord Integration
        include(implementation("com.kohlschutter.junixsocket:junixsocket-common:2.7.0"))
        include(implementation("com.kohlschutter.junixsocket:junixsocket-native-common:2.7.0"))
        include(implementation("com.jagrosh.discordipc:DiscordIPC:0.6.0"))
        // Mods libs
        modImplementation "maven.modrinth:replaymod:${project.replaymod}"
        modImplementation "maven.modrinth:plasmo-voice:${project.plasmovoice}"
        modImplementation "maven.modrinth:simple-voice-chat:${project.svc}"
        modImplementation "ru.kelcuprum.alinlib:AlinLib-common:${project.alinlib}"
        modImplementation "ru.kelcuprum.waterplayer:WaterPlayer:${project.waterplayer}"
        include(implementation("meteordevelopment:starscript:0.2.2"))

        subprojects.each {
            implementation project(path: ":${it.name}", configuration: 'namedElements')
            implementation project(path: ":${it.name}", configuration: 'shade')
            include project("${it.name}:")
        }
    }

    machete {
        ignoredTasks.add("shadowJar")
        ignoredTasks.add("jar")
    }

}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        mavenCentral()
        maven { url = "https://maven.terraformersmc.com/releases" }
        maven { url = "https://maven.neoforged.net/releases/" } // NEOFORGE
        maven { url = "https://maven.meteordev.org/releases" }
        maven { url = 'https://repo.kelcuprum.ru/snapshots' }
        maven { url = 'https://repo.kelcuprum.ru/releases' }
        maven { url = 'https://repo.kelcuprum.ru/forks' }
        maven { url = 'https://repo.kelcuprum.ru/test' }
        maven { url = "https://api.modrinth.com/maven" }
        maven { url = "https://maven.parchmentmc.org" } // MAPPINGS
        maven { url = "https://jitpack.io/" }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release.set(17)
    }

    java {
        withSourcesJar()
    }
}